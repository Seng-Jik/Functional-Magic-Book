# Lambda 演算（λ-Calculus）

## 三条基本规则

我们可以使用以下三条规则表示任何计算过程：

1. 可以给值起一个名字，当写上名字时，代表这个值。
2. 形如表达式 “λx.x+1”，它表示一个数学上的函数“f(x) = x + 1”，这个函数是一个值。
3. 如果我们具有一个值 f，f的值为“λx.x+1”，那么表达式“f 1”或“(λx.x+1) 1”的值就相当于将1带入x后进行计算，类似数学上从“f(x) = x + 1”带入“x = 1”后得到“f(1) = 1 + 1”，最终得到的结果为2，此结果也是一个值。（这一过程被称作Apply。）

通过这三条规则，我们有了一个强大的工具“Lambda演算（λ-Calculus）”，用于表示计算的过程了。

以下是一个示例，这个例子将会计算1、2、3三个值的平均值：

```lambda
(λx. (λy. (λz. (x + y + z) / 3))) 1 2 3
```

下面这段代码使用F#编程语言写成，乍一看似乎和Lambda演算的写法有很大不同，但实际上只是把λ符号换成了fun，把点号换成了->而已。

```fsharp
(fun x -> fun y -> fun z -> (x + y + z) / 3.0) 1.0 2.0 3.0
```

但实际上这一段代码并没有指示计算机输出结果，如果想输出结果，可以根据上一章学到的let与printfn来改写成这样：

```fsharp
let a = (fun x -> fun y -> fun z -> (x + y + z) / 3.0) 1.0 2.0 3.0
printfn "%A" a
```

## 那么，F#编程语言中的let是什么呢？

在上文中，在F#编程语言里，我们发现了一个let关键字，似乎是把右面那一长串表达式绑定到了名字a上，看起来好像这是编程语言固有的机制，不是三条规则内的东西，但实际上let关键字只不过是三条规则的衍生品而已。

观察以下F#编程语言代码和λ演算表达式：

```fsharp
let a = 1 + 2 + 3
a * 2
```

```lambda
(λa. a * 2) (1 + 2 + 3)
```

似乎两者起到了同样的作用，计算的行为一致，也确实都把1 + 2 + 3的结果绑定到了a上。

假设在λ演算的环境中已经存在名字let，它的值如下：

```lambda
λx. (λcontinuation. continuation x)
```

那么，我们可以写出以下的λ演算表达式：

```lambda
let (1 + 2 + 3) (λa. a * 2)
```

是不是看起来就像F#代码一样了呢？

然而F#编程语言中let的完整写法是这样的：

```fsharp
let a = 1 + 2 + 3 in a * 2
```

而实际上，和F#编程语言中以下写法等价：

```lambda
(fun a -> a * 2) (1 + 2 + 3)
```

当然，我们也可以定义一个函数，由于let是关键字，所以这里我们定义为bind：

```fsharp
let bind = fun x -> fun continuation -> continuation x
bind (1 + 2 + 3) (fun a -> a * 2)
```

看起来是不是和上面λ演算表达式中的写法所表达的内容一致呢？

总结下来，let关键字的工作方式无非也就是以下两个步骤：

```fsharp
let a = 1 + 2 + 3 in a * 2
```

1. 把这in之后所有的计算打包成一个函数，并且这个函数有一个参数，参数的名字为“=”号左边的名字，在这个例子中，“a * 2”被打包成了函数“(fun a -> a * 2)”。
2. “=”号右边的表达式被包上括号，放到了步骤1打包出的函数的右边，在当前这个例子里，变成了“(fun a -> a * 2) (1 + 2 + 3)”。

这一过程叫“绑定（Binding）”，将一个值绑定到一个名字上。

## printfn的未解之谜

观察以下F#编程语言的代码：

```fsharp
let a = 1
printfn "%A" a
let b = 1
printfn "%A" b
```

似乎我们很难写出它的λ演算表达式，就好像连续两个let，中间隔了一个printfn“语句”一样，首先我们写出它的完整形式。

```fsharp
let a = 1 in 
do printfn "%A" a in
let b = 1 in
printfn "%A"
```

这时候你可能会想了，do又是个什么玩意儿？   
可能写成以下形式，你就会很熟悉了：

```fsharp
let a = 1 in
let something = printfn "%A" a in
let b = 2 in
printfn "%A" b
```

尽管printfn看起来像是语句，但它实际上是一个返回“()”的函数，尽管“()”不是一个数字，但它在F#编程语言中仍然是一个值，代表“什么也没有”。

这样，我们便可以使用λ演算表达式来表示上面的代码了：

```lambda
(λbind.											;; 需要从后面引入bind
	bind 1 (λa.									;; 将1绑定到a上
	bind (printfn "%A" a) (λsomething.			;; 将printfn "%A" a绑定到something上
	bind 2 (λb.									;; 将2绑定到b上
	printfn "%A" b)))							;; 计算最后的printfn "%A" b
) (λx. (λcontinuation. continuation x))			;; bind函数的定义
```

## if表达式与布尔逻辑

考虑一个函数clamp(max, min, v)，当v大于max时，函数值为max，当v小于min时，函数值为min，当v处于max和min之间时，函数值为v。   
这种情况下，我们便需要if表达式，正如以下F#代码：

```fsharp
let clamp = fun max -> fun min -> fun v ->
	if v > max then max		// 如果 v > max，则返回max
	else if v < min then min	// 如果 v < min，则返回min
	else v				// 其他情况返回v
```

if表达式具有以下结构：
`if <条件> then <成立时要计算的表达式> else <不成立时要计算的表达式>`

因为表达式可以嵌套，所以else后面可以再跟if，便形成了类似以下这种结构：
```fsharp
if <条件1> then <条件1成立时>
else if <条件2> then <条件2成立时>
else if <条件3> then <条件3成立时>
else <以上条件均不成立时>
```

加上括号的话可以看作这样的结构：
```fsharp
if <条件1> then <条件1成立时>
else (
	if <条件2> then <条件2成立时>
	else (
		if <条件3> then <条件3成立时>
		else <以上条件均不成立时>))
```

似乎在λ演算的三条规则当中，并没有关于分支的规则，但实际上分支也是由这三条规则推演而来的。

首先将以上F#代码修改成这种更便于思考的形式：

```fsharp
let clamp = fun max -> fun min -> fun v ->
	if v > max then max
	else
		if v < min then min
		else v
```

在F#中，比较运算符“>”和“<”返回的是true和false，似乎F#中的true和false并没给出更详细的定义。   
但是在λ演算中，我们可以作出以下定义：

true的定义为：
```lambda
(λa. (λb. a))
```

false的定义为:
```lambda
(λa. (λb. b))
```

也就是说，对于true，它总是返回第一个参数，对于false，它总是返回第二个参数，于是我们便可以利用true和false两个函数的特性来定义clamp：

```lambda
(λmax. (λmin. (λv.
	(v > max) 
		max
		((v < min) 
			min
			v))))
```

同样，在F#中，我们可以写出以下代码：

```fsharp
let mapBoolean = fun v ->		// 这个函数用来把F#中的true和false变换成上面λ演算表达式中的true函数和false函数
	if v = true then (fun a -> fun b -> a)	// 这里会产生警告“'b' is unused.”，可以使用_替换掉b，“_”的意思是不使用这个参数。
	else (fun a -> fun b -> b)

let clamp = fun max -> fun min -> fun v ->
	(mapBoolean (v > max)) 
		max
		((mapBoolean (v < max))
			min
			v)
```

这样，我们便得到了true和false的定义。   
F#的if表达式依然由三条规则推演而成：

1. 将if和then之间的表达式进行求值，得到true或false，并替换成fun a -> fun b -> a和fun a -> fun b -> b。
2. 将then和else之间的表达式提取出来，并且加上括号，放在步骤1产生的函数后面，作为第一个参数。
3. 将else后面的表达式提取出来，并且加上括号，放在步骤2产生的值后面，作为步骤1产生的函数的第二个参数。

### 如果没有else分支的话

对于F#中的if表达式，还有一种特殊情况，就是它可以没有else分支，比如以下代码：

```fsharp
let a = 1
if a = 1 then 
	printfn "a equals 1!"
```

F#对于这种情况的if，要求分支内的表达式返回的值必须是()，因为它将会被补全为以下形式：

```fsharp
let a = 1
if a = 1 then
	printfn "a equals 1!"	// 此表达式返回 ()
else
	()
```

这里()表示一种“没有意义”的值。

这样处理之后，它又是可以通过λ演算来表达的形式了。

### 逻辑运算

对于true和false，我们还需要以下四种运算：

* 非
* 与
* 或
* 异或

对于“非”运算，当传入true时，将会返回false，当传入false时，将会返回true，因此它的定义非常简单，只需要将false和true以相反的顺序传入即可。
以下是“非”运算的定义：

```lambda
λv. v false true
```

对于“与”运算，需要两个参数，仅当两个参数都为true时，返回才为true，否则为false。
以下为“与”运算的定义：

```lambda
λa. (λb. a (b true false) false))
```

如果`b`为`false`，则得到的结果是`a false false`，最终得到的是`false`。
如果`b`为`true`，则得到的结果为`a true false`，此时若`a`为`true`，则最终结果为`true`，反之得到`false`。

化简后为：

```lambda
λa. (λb. a b false)
```

对于“或”运算，两个参数之中任一参数为true，则它为true，如果两者均为false，才返回false。
以下为“或”运算的定义：

```lambda
λa. (λb. a true (b true false))
```

如果`b`为`false`，则得到的结果是`a true false`，若a为`true`，则最终结果为`true`，否则为`false`。
如果`b`为`true`，则得到的结果为`a true true`，此时无论`a`是`true`还是`false`，最终结果都为`true`。

化简后为：

```lambda
λa. (λb. a true b)
```

对于“异或”运算，要求两者参数中仅有一个true时才返回true，两者参数均为true或均为false时，返回false。
以下为“异或”运算的定义：

```lambda
λa. (λb. a (b false true) (b true false))
```

令“非”运算为not，则化简“异或”后为：

```lambda
λa. (λb. a (not b) b)
```

可使用F#编程语言写出以上定义：

```fsharp
let myTrue = fun a -> fun _ -> a
let myFalse = fun _ -> fun b -> b

let myNot = fun v -> v myFalse myTrue
let myAnd = fun a -> fun b -> a b myFalse
let myOr = fun a -> fun b -> a myTrue b
let myXor = fun a -> fun b -> a (myNot b) b
```

## F#编程语言中函数的各种简化写法

我们有以下函数，将x和y加起来：
```fsharp
let f = fun x -> fun y -> x + y
```

对于这种情况，我们可以将其化简为以下写法：
```fsharp
let f = fun x y -> x + y
```

更进一步地，可以化简为以下写法：
```fsharp
let f x y = x + y
```

可以根据需要挑选最适合的写法。

## 扩展阅读：丘奇计数

对于Lambda演算，可以使用“丘奇计数（Church Numerals）”的方法来表示自然数，我们可以通过以下方式定义自然数：

0的定义

```lambda
λf. (λx. x)
```

```fsharp
fun f -> fun x -> x
```

1的定义

```lambda
λf. (λx. f x)
```

```fsharp
fun f -> fun x -> f x
```

2的定义

```lambda
λf. (λx. f (f x))
```

```fsharp
fun f -> fun x -> f (f x)
```

3的定义

```lambda
λf (λx. f (f (f x)))
```

```fsharp
fun f -> fun x -> f (f (f x))
```

以此类推。

观察以上定义得出，可以通过f对x进行的运算次数来进行计数，这便是“丘奇计数法”。

类比到自然数集上，相当于使用对0执行“加1”操作的次数来进行计数。

但是至此我们依然无法用它计数，因为这些数字之间没有产生联系，其中最重要的一个联系便是“加上一”。

下面这个函数将会把一个丘奇数m加上一：

```lambda
λm. (λf. (λx. m f (f x)))
```

```fsharp
fun m ->
	fun f -> fun x ->
		m f (f x)
```

这样，我们就能对一个丘奇数进行一个加1的操作。

有了“加一”，我们可以由此引申出“加n”的操作了，其定义如下：

```lambda
λm. (λn. (λf. (λx. m f (n f x))))
```

```fsharp
fun m -> fun n ->
	fun f -> fun x ->
		m f (n f x)
```

以至于我们还能定义出乘法：

```lambda
λm. (λn. (λf. (λx. m (n f) x)))
```

注意这里，我们需要对`x`做m * n次`f`操作，所以我们可以先让`f`做`n`次操作，也就是`(λx. n f x)`，可以简写为`(n f)`，之后再将让`(n f)`做`m`次操作，即可得到`m * n`。

```fsharp
fun m -> fun n ->
	fun f -> fun x ->
		m (n f) x
```

注意：由于F#编程语言是静态类型的编程语言，丘奇数的乘法并非在所有情况下都可使用，关于“类型”请见后文。


