# 递归

## 回到最初的1+2+...+10000的问题
现在，我们要开始回顾最开始的“`1+2+...+10000`”的问题了，我们可以整理出以下思路：

有一个函数`f`，这个函数是一个分段函数，当`x = 0`时，`f(x) = 0`，当`x > 0`时，`f(x) = x + f(x - 1)`，其它情况下`f(x)`不存在。

此时，`f(10000)`即是这个问题的解。

我们可以很轻松地编写出以下F#编程语言编写的代码：

```fsharp
let f =	fun x ->        // 设函数f(x)
    if x = 0 then 0     // 若x = 0
    else if x > 0 then x + f (x - 1)    // 若x > 0
    else invalidArg "x" "f(x) does not exists!"  // 其他情况f(x)不存在
    
printf "%d" (f 10000)
```

为了方便描述，我们在这里将会引入“模式匹配（Partten Matching）”语法来更清晰地描述这个函数，关于模式匹配的语法，请参阅*https://docs.microsoft.com/zh-cn/dotnet/fsharp/language-reference/pattern-matching*，之后我们便可写出这样的代码：

```fsharp
let f = function
| 0 -> 0
| x when x > 0 -> x + f (x - 1)
| _ -> invalidArg "x" "f(x) does not exists!"

printf "%d" (f 10000)
```

但F#编译器却报了错误“没有找到对`f`的定义”。

根据**Lambda演算**一章中的知识，let不过是一个函数调用，我们将它展开一下，可以发现，它变成了这样的代码：

```fsharp
(fun f -> printf "%d" (f 10000)) (
    function
    | 0 -> 0
    | x when x > 0 -> x + f (x - 1)
    | _ -> invalidArg "x" "f(x) does not exists!")
```

而此处很明显就可以发现问题，在这里，`f`确实是没有定义的。

当一个函数引用自身时，这种情况被称作“递归”，而使用let语法，是不能定义一个递归函数的，因为其展开形式中，此函数的定义所在的作用域中，并没有它自身。

F#提供了`rec`关键字，使得你可以在函数的定义中访问它自身：

```fsharp
let rec f = function
| 0 -> 0
| x when x > 0 -> x + f (x - 1)
| _ -> invalidArg "x" "f(x) does not exists!"

printf "%d" (f 10000)
```

这里，函数便可以正常引用其自身，并求出计算结果了。

## 扩展阅读：Y组合子

你也可以使用Y组合子来定义一个递归函数，你可以以这种方式来使用它：

```fsharp
let f = Y (fun self x ->
	match x with
    | 0 -> 0
    | x when x > 0 -> x + self (x - 1)
    | _ -> invalidArg "x" "f(x) does not exists!")
```

很明显可以看出，Y组合子接受了一个函数`f`和`f`的参数`x`，之后Y组合子便会向`f`传入f自身及参数`x`，它看起来可以用以下方式在F#中写出：

```fsharp
let rec Y f x = f (Y f) x
```

但是，它依然使用了`rec`关键字，但它可以很好地解释Y组合子的概念，你可以用λ-演算的形式在不使用`rec`关键字的情况下写出Y组合子的正确形式：

```lambda
λf. (λx. (f (x x)) λx. (f (x x)))
```

